#!/bin/bash

light="#DCBCBC"
light_highlight="#C79999"
mid="#B97C7C"
mid_highlight="#A25050"
dark="#8F2727"
dark_highlight="#7C0000"

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 4
set output "gauss_marginal.eps"

set border 3 lw 2

set lmargin screen 0.1

set title ""

set xtics scale 0
set xlabel 'x_{1}'
set format x ''
set xrange[-4.5:4.5]

set ytics scale 0
set ylabel ''
set format y ''
set yrange[0:0.5]

rho=0.8
margin_gauss(x) = exp(-0.5 * x**2 / 1) / sqrt(2 * pi * 1)

plot margin_gauss(x) w filledcurve lt 1 lw 1 lc rgb "$dark" notitle

END

exit

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 6
set output "gauss_conditional.eps"

set multiplot
set size 1, 0.8
set origin 0, 0.2

set border 3 lw 2

set title ""

set xtics scale 0
set xlabel 'x_{1}'
set format x ''
set xrange[-4.5:4.5]

set ytics scale 0
set ylabel 'x_{2}'
set format y ''
set yrange[-4.5:4.5]

do for [x1=-4:4] {
  set arrow from -x1, -4.5 to -x1, 4.5 dt '-' lt 1 lw 5 lc rgb "#CCCCCC" nohead
}

set parametric
set trange [-4.5:4.5]
set samples 250

rho = 0.8
prec = 1 / sqrt(1 - rho**2)
cond_gauss(x2, x1) = 0.5 * exp( -0.5 * prec * (x2 - rho * x1)**2 )

plot for [x1=-4:4]  x1 - cond_gauss(t, x1), t w l lt 1 lw 7 lc rgb "$dark" notitle

set size 1, 0.2
set origin 0, 0

set border 3 lw 2

set lmargin screen 0.1

set title ""

unset arrow

set xtics scale 0
set xlabel ''
set format x ''
set xrange[-4.5:4.5]

set ytics scale 0
set ylabel ''
set format y ''
set yrange[0:0.5]

rho=0.8
margin_gauss(x) = exp(-0.5 * x**2 / 1) / sqrt(2 * pi * 1)

unset parametric

plot margin_gauss(x) w filledcurve lt 1 lw 1 lc rgb "$dark" notitle

END

exit

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 4
set output "gauss_joint.eps"

set border 10  lw 4

set tmargin screen 0.95
#set bmargin
set lmargin screen 0.1
set rmargin screen 0.95

set pm3d map implicit at s

set title ""

set xtics scale 0
set xlabel 'x_{1}' offset 0, 1.5
set format x ''
set xrange[-4.5:4.5]

set ytics scale 0
set ylabel 'x_{2}'
set format y ''
set yrange[-4.5:4.5]

set zlabel ''
set zrange[1e-3:1]
unset cbtics

unset colorbox

set palette rgbformulae 8, 9, 9 negative

set samples 250
set isosamples 250

rho=0.8

splot exp( - 0.5 * (x * x - 2 * rho * x * y + y * y) / (1 - rho * rho) ) notitle

END

gnuplot << END

set terminal postscript enhanced eps color font 'Times-Roman, 35' size 6, 4
set output "gauss_margin.eps"

set border 0

set title ""

set lmargin screen 0.125
set rmargin screen 0.875

set xtics scale 0
set xlabel ''
set format x ''
set xrange[-4.5:4.5]

set ytics scale 0
set ylabel ''
set format y ''
set yrange[0:3]

rho=0.8
gauss(x, mu, sigma2) = exp(-0.5 * (x - mu)**2 / sigma2) / sqrt(2 * pi * sigma2)

plot gauss(x, 0, 1) w filledcurve lt 1 lw 1 lc rgb "$dark" notitle

END
